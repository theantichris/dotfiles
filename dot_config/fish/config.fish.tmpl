source ~/.config/fish/functions/done.fish

function fish_greeting
	fastfetch
end

# Format man pages
set -x MANROFFOPT -c
set -x MANPAGER "sh -c 'col -bx | bat -l man -p'"

# Environment setup

## Set settings for done
set -U __done_min_cmd_duration 10000
set -U __done_notification_urgency_level_low

## Set Bitwarden SSH agent
set -Ux SSH_AUTH_SOCK "$HOME/.bitwarden-ssh-agent.sock"

## Apply .profile: use this to put fish compatible .profile stuff in
if test -f ~/.fish_profile
	source ~/.fish_profile
end

## Add ~/.local/bin to PATH
if test -f ~/.local/bin
	if not contains -- ~/.local/bin $PATH
		set -p PATH ~/.local/bin
	end
end

## Add depot_tools to PATH
if test -d ~/Applications/depot_tools
	if not contains -- ~/Applications/depot_tools $PATH
		set -p PATH ~/Applications/depot_tools
	end
end

## Add Go bin to PATH
if test -f ~/go/bin
	if not contains -- ~/go/bin $PATH
		set -p PATH ~/go/bin
	end
end

# CachyOS functions

## Functions needed for !! and !$

function __history_previous_command
	switch (commandline -t)
	case "!"
		commandline -t $history[1]; commandline -f repaint
	case "*"
		commandline -i !
	end
end

function __history_previous_command_arguments
	switch (commandline -t)
	case "!"
		commandline -t ""
		commandline -f history-token-search-backward
	case "*"
		commandline -i '$'
	end
end

if [ "$fish_key_bindings" = fish_vi_key_bindings ];
	bind -Minsert ! __history_previous_command
	bind -Minsert '$' __history_previous_command_arguments
else
	bind ! __history_previous_command
	bind '$' __history_previous_command_arguments
end

## Fish command history

function history
	builtin history --show-time='%F %T '
end

function backup --argument filename
	cp $filename $filename.bak
end

# Copy DIR1 DIR2

function copy
	set count (count $argv | tr -d \n)
	if test "$count" = 2; and test -d "$argv[1]"
		set from (echo $argv[1] | trim-right /)
		set to (echo $argv[2])
		command cp -r $from $to
	else
		command cp $argv
	end
end
